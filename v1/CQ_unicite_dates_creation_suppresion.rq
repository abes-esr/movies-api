#+ endpoint: https://movies.abes.fr/proxy/wdqs/bigdata/namespace/wdq/sparql?
#+ endpoint_in_url: False
#+ description: Liste les entités qui ont plusieurs date de création ou date de suppression. Il ne peut y avoir qu'une seule date de création et de suppression par entité.

PREFIX wdt: <https://movies.abes.fr/prop/direct/>
PREFIX wd: <https://movies.abes.fr/entity/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX p: <https://movies.abes.fr/prop/>
PREFIX pq: <https://movies.abes.fr/prop/qualifier/>

SELECT ?statement ?problem (count(?c) as ?count) WHERE {
  {
    ?statement wdt:P10 ?c.
    BIND("Plusieur dates de création" AS ?problem)
  } UNION {
    ?statement wdt:P54 ?c.
    BIND("Plusieur dates de suppression" AS ?problem)
  }
} GROUP BY ?statement ?problem
HAVING (?count > 2) # > 2 car le format de date EDTF créer deux dates pour chaque statement !
