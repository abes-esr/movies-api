#+ endpoint: https://movies.abes.fr/proxy/wdqs/bigdata/namespace/wdq/sparql?
#+ endpoint_in_url: False
#+ description: Cette requête permet de trouver le ou les successeurs d'un établissement habilité à délivrer le doctorat

PREFIX wdt: <https://movies.abes.fr/prop/direct/>
PREFIX wd: <https://movies.abes.fr/entity/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX p: <https://movies.abes.fr/prop/>
PREFIX pq: <https://movies.abes.fr/prop/qualifier/>

SELECT DISTINCT ?etabLabel ?codeEtabSource ?codeEtabCible  WHERE {
  BIND(?_codeEtabSource AS ?codeEtabSource)
  ?etab wdt:P9 ?codeEtabSource.

  ?etab p:P22 ?hab. # Récupération de l'habilitation
  	
  ?hab <https://movies.abes.fr/prop/statement/P22> wd:Q20; # en cas de transfert
       pq:P1 ?beneficiaire.
  
  ?beneficiaire wdt:P9 ?codeEtabCible.
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
